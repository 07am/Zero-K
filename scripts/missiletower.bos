#define TA			// This is a TA script

#include "constants.h"

piece  base, body, turret, launcher1, launcher2, firepoint1, firepoint2;

static-var  gun, aiming;

#define SIG_AIM				2
#define SIG_SPIN			4

#define RESTORE_DELAY		2000

// Signal definitions

SmokeUnit(healthpercent, sleeptime, smoketype)
{
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while( TRUE )
	{
		healthpercent = get HEALTH;
		if( healthpercent < 66 )
		{
			smoketype = 256 | 2;
			if( Rand( 1, 66 ) < healthpercent )
			{
				smoketype = 256 | 1;
			}
			emit-sfx smoketype from base;
		}
		sleeptime = healthpercent * 50;
		if( sleeptime < 200 )
		{
			sleeptime = 200;
		}
		sleep sleeptime;
	}
	return (0);
}

Spinner()
{
	signal SIG_SPIN;
	set-signal-mask SIG_SPIN;
	while( get BUILD_PERCENT_LEFT )
	{
		sleep 400;
	}
	while (!aiming)
	{
		turn turret to y-axis <0> speed <50.000000>;
		wait-for-turn turret around y-axis;
		turn turret to y-axis <240> speed <50.000000>;
		wait-for-turn turret around y-axis;
		turn turret to y-axis <120> speed <50.000000>;
		wait-for-turn turret around y-axis;
	}
}

RestoreAfterDelay()
{
	sleep RESTORE_DELAY;
	turn launcher1 to x-axis <-30.000000> speed <50.000000>;
	turn launcher2 to x-axis <-30.000000> speed <50.000000>;
	aiming = FALSE;
	start-script Spinner();
}

AimPrimary(heading, pitch)
{
	sleep 10;
	signal SIG_AIM;
	set-signal-mask SIG_AIM;
	aiming = TRUE;
	turn turret to y-axis heading speed <350.000000>;
	turn launcher1 to x-axis <0.000000> - pitch speed <350.000000>;
	turn launcher2 to x-axis <0.000000> - pitch speed <350.000000>;
	wait-for-turn turret around y-axis;
	wait-for-turn launcher1 around x-axis;
	
	start-script RestoreAfterDelay(RESTORE_DELAY);
	
	return (1);
}

Shot1()
{
	if (gun) {
		emit-sfx UNIT_SFX1 from firepoint2;
	}
	else {
		emit-sfx UNIT_SFX1 from firepoint1;
	}
	gun = !gun;
}


AimFromPrimary(piecenum)
{
	piecenum = turret;
}

QueryPrimary(piecenum)
{
	if(gun)
	{
		piecenum = firepoint1;
	}
	if(!gun)
	{
		piecenum = firepoint2;
	}
}

SweetSpot(piecenum)
{
	piecenum = base;
}

Create()
{
	gun = TRUE;
	start-script SmokeUnit();
	start-script RestoreAfterDelay();
}

Killed(severity, corpsetype)
{
	if( severity <= 25 )
	{
		corpsetype = 1;
		explode launcher1 type BITMAPONLY | BITMAP1;
		explode launcher2 type BITMAPONLY | BITMAP1;
		explode base type BITMAPONLY | BITMAP2;
		explode turret type BITMAPONLY | BITMAP5;
		return (0);
	}
	if( severity <= 50 )
	{
		corpsetype = 2;
		explode launcher1 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
		explode launcher2 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
		explode base type BITMAPONLY | BITMAP2;
		explode turret type BITMAPONLY | BITMAP2;
		return (0);
	}
	if( severity <= 99 )
	{
		corpsetype = 3;
		explode launcher1 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
		explode launcher2 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
		explode base type BITMAPONLY | BITMAP2;
		explode turret type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP5;
		return (0);
	}
	corpsetype = 3;
		explode launcher1 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
		explode launcher2 type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP1;
		explode base type BITMAPONLY | BITMAP2;
		explode turret type FALL | SMOKE | FIRE | EXPLODE_ON_HIT | BITMAP5;
}
